plugins {
    id 'nebula.release' version '15.2.0'
    id 'base'

    id 'nebula.maven-publish' version '17.3.2' apply false
    id 'nebula.javadoc-jar' version '17.3.2' apply false
    id 'nebula.source-jar' version '17.3.2' apply false
    id 'nebula.contacts' version '5.1.0' apply false
    //id 'nebula.info' version '9.1.2' apply false
    id "com.github.hierynomus.license" version "0.16.1"
    id 'org.ajoberstar.git-publish' version '3.0.1'
    id 'org.ajoberstar.grgit' version '4.1.1'
    id "com.github.ben-manes.versions" version "0.36.0"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group 'com.blackbuild.groovycps'
description 'Unit-test your shared Jenkins libraries'

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {
    apply plugin: 'nebula.maven-publish'
    apply plugin: 'nebula.javadoc-jar'
    apply plugin: 'nebula.source-jar'
    apply plugin: 'nebula.release'
    apply plugin: 'nebula.contacts'
    // apply plugin: 'nebula.info'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'signing'
    apply plugin: 'com.github.hierynomus.license'

    group rootProject.group

    repositories {
        mavenCentral()
        maven {
            url "https://repo.jenkins-ci.org/releases/"
        }
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }
    jacoco {
        toolVersion = "0.8.2"
    }

    contacts {
        'stephan@blackbuild.com' {
            moniker 'Stephan Pauxberger'
            github 'pauxus'
        }
    }

    publishing {
        publications {
            nebula(MavenPublication) {
                suppressPomMetadataWarningsFor('testFixturesApiElements')
                suppressPomMetadataWarningsFor('testFixturesRuntimeElements')
                pom {
                    url = "https://github.com/klum-dsl/klum-ast"
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'http://opensource.org/licenses/MIT'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:blackbuild/groovy-cps-util.git'
                        developerConnection = 'scm:git:git@github.com:blackbuild/groovy-cps-util.git'
                        url = 'https://github.com/blackbuild/groovy-cps-util'
                    }
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") || gradle.taskGraph.hasTask("publishToMavenLocal") }
        sign publishing.publications.nebula
    }

    license {
        header = rootProject.file("LICENSE")
        mapping("java", "SLASHSTAR_STYLE")
        mapping("groovy", "SLASHSTAR_STYLE")
        strictCheck(true)
    }

}


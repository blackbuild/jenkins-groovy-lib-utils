plugins {
    id 'groovy'
    id 'java-library'
    id 'jacoco'
    id 'signing'
    id 'com.github.hierynomus.license'
    id 'maven-publish'
}

group rootProject.group
description = "Base Test clases for use in Jenkins Unit Testing"

repositories {
    mavenCentral()
    maven {
        url "https://repo.jenkins-ci.org/public/"
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
    withJavadocJar()
    withSourcesJar()
}

jacoco {
    toolVersion = "0.8.2"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom { pom ->
                name = project.name
                description = project.description
                rootProject.configurePom(pom)
            }
        }
    }
}

license {
    header = rootProject.file("LICENSE")
    mapping("java", "SLASHSTAR_STYLE")
    mapping("groovy", "SLASHSTAR_STYLE")
    strictCheck(true)
}

dependencies {
    compileOnly libs.groovy
    compileOnly libs.groovycps
    compileOnly "org.jenkins-ci.main:jenkins-core:2.319.1"
    compileOnly "org.jenkins-ci.plugins.workflow:workflow-cps:2648.va9433432b33c@jar"
    compileOnly "org.jenkins-ci.plugins.workflow:workflow-support:804.vba10a18a1476@jar"
    compileOnly "org.jenkins-ci.plugins.workflow:workflow-api:1108.v57edf648f5d4@jar"
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'

    api libs.bundles.spockJenkins
}

signing {
    required { gradle.taskGraph.hasTask("publish") || gradle.taskGraph.hasTask("publishToMavenLocal") }
    sign publishing.publications.mavenJava
}

configurations.testImplementation.extendsFrom configurations.compileOnly